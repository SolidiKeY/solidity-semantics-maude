fmod WRITE-READ-SORTS is
  sort Map .
  sort Key .
  sort Value .

  op defaultValue : -> Value .
  op defaultMap : -> Map .

  op read : Map Key -> Value .
  op write : Map Key Value -> Map .

  vars key keyWrite keyRead : Key .
  var value : Value .
  var map : Map .

  eq read(defaultMap, key) = defaultValue .
endfm

fmod WRITE-READ is
  inc WRITE-READ-SORTS .

  vars key keyWrite keyRead : Key .
  var value : Value .
  var map : Map .

  eq read(write(map, key, value), key) = value .

  ceq read(write(map, keyWrite, value), keyRead) = read(map, keyRead)
    if keyWrite =/= keyRead .
endfm

fmod WRITE-SUBSTITUTE is
  inc WRITE-READ .

  vars key keyWrite newKey : Key .
  var value : Value .
  var map : Map .

  op removeFirstKey : Map Key -> Map .

  eq removeFirstKey(write(map, key, value), key) = map .
  ceq removeFirstKey(write(map, keyWrite, value), newKey) = write(removeFirstKey(map, newKey), keyWrite, value)
    if keyWrite =/= newKey .

  op keyInMap : Map Key -> Bool .

  eq keyInMap(defaultMap, key) = false .
  eq keyInMap(write(map, key, value), key) = true .
  ceq keyInMap(write(map, keyWrite, value), newKey) = keyInMap(map, newKey)
    if keyWrite =/= newKey .

  ceq write(map, key, value) = write(removeFirstKey(map, key), key, value)
    if keyInMap(map, key) .
endfm

fmod WRITE-READ-WITH-EXAMPLES is
  pr WRITE-READ .
  op k1 : -> Key .
  op k2 : -> Key .
  op v1 : -> Value .
  op v2 : -> Value .

  op emptyMap : -> Map .
endfm
