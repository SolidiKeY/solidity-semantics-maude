fmod STATIC-ARRAY is
    pr INT .

    sorts Map Array MapArray Value .
    subsort Map Array < MapArray .

    op read  : MapArray Int    -> Value .
    op write : Array Int Value -> Array .
    op write : Map   Int Value -> Map   .

    op Array  : Int Map -> Array .
    op length : Array -> Int .
    op getMap : Array -> Map .

    vars i j size : Int .
    var array : Array .
    var val : Value .
    var map : Map .

    eq read(array, i) = read(getMap(array), i) .

    eq read(write(map, i, val), i) = val .
    ceq read(write(map, i, val), j) = read(map, j)
        if i =/= j .

    eq length(Array(size, map)) = size .
    eq getMap(Array(size, map)) = map .

    eq write(array, i, val) = Array(length(array), write(getMap(array), i, val)) .

    op emptyMap : -> Map .
    op defaultValue : -> Value .
    op emptyArray : Int -> Array .

    eq read(emptyMap, i) = defaultValue .
    eq emptyArray(size) = Array(size, emptyMap) .
endfm

fmod TEST-STATIC-ARRAY is
    pr STATIC-ARRAY .

    ops v1 v2 : -> Value .

    ops exArray unknown unknownArray : -> Array .
    eq exArray = write(Array(10, emptyMap), 1, v1) .

    eq unknown = write(write(unknownArray, 1, v1), 2, v2) .
endfm

red read(Array(10, emptyMap), 1) .

red exArray .

red read(exArray, 1) .

red read(exArray, 2) .

red length(Array(20, emptyMap)) .

red length(exArray) .

red read(unknown, 0) .
red read(unknown, 1) .
red read(unknown, 2) .

red length(unknown) .
red length(write(unknownArray, 1, v1)) .
red length(write(unknownArray, 1, v1)) == length(unknown) .
