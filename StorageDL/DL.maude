fmod DYNAMIC is
    pr NAT .
    sorts Type KeySimple KeyComplex Key FieldStorage FieldConcrete Field Value
        Updates Assign Program NeProgram Left Right
        KeyStorage KeyConcrete KeyValue KeyValuePair KeyStoragePath
        KeyStorageComplex KeyConcreteComplex KeySimpleStorage KeySimpleStorageBoth KeySimpleConcrete .

    subsort KeyValuePair < Updates .
    subsort FieldStorage FieldConcrete < Field .
    subsort KeySimpleStorage KeyStoragePath KeySimpleConcrete < KeySimple .
    subsort KeySimpleStorage KeyStoragePath < KeySimpleStorageBoth .
    subsort KeyStorageComplex KeyConcreteComplex < KeyComplex .
    subsort KeySimple KeyComplex < Key .
    subsort KeySimpleConcrete < Value .
    subsort Key Value < KeyValue .
    subsort KeyValue < Left Right .
    subsort Assign < NeProgram < Program .

    op {_}_ : Updates Updates -> Updates [format (ni s s ++t d)] .
    op _||_ : Updates Updates -> Updates [prec 33 assoc] .
    op _:=_ : Key KeyValue -> KeyValuePair [format (b! o o o) prec 31] .
    op _,_ : Key FieldStorage -> KeyStorageComplex [prec 30] .
    op _,_ : Key FieldConcrete -> KeyConcreteComplex [prec 30] .
    op <_> : Program -> Updates [format (nt d d d)] .
    ops <> nil : -> Program .
    op nil : -> Updates .
    op _;_ : Program Program -> Program [ctor assoc id: nil prec 37] .
    op _;_ : NeProgram Program -> NeProgram [ctor ditto] .
    op _;_ : Program NeProgram -> NeProgram [ctor ditto] .
    op _<-_ : Left Right -> Assign [prec 35] .
    ops Person Account Int : -> Type .
    op _storage_ : Type Key -> Left [prec 31] .

    op Storage : -> KeySimple [format (b! o)] .
    op store : KeySimple Key KeyValue -> KeyValue .
    op select : KeySimple Key -> KeyValue .
    op _:_ : KeySimple Field -> KeySimple [prec 30] .
    op Cp : Program -> KeySimple .
    op C  : Nat -> KeySimple .
    op K : -> KeySimpleConcrete .
    op Ks : -> KeyStoragePath .

    vars sL sRR : KeySimple .
    var a : Field .
    var aC : FieldConcrete .
    var aS : FieldStorage .
    var cL cR : KeyComplex .
    var kvL kvR : KeyValue .
    var sLs sR : Key .
    var prog : Program .
    var updated : Updates .
    var val : Value .
    var kval : KeyValue .
    var type : Type .
    var assign : Assign .
    var n : Nat .
    var stC : KeyStorageComplex .
    var ksStorL : KeySimpleStorage .
    var ksStorB ksStorR : KeySimpleStorageBoth .
    var kcC : KeyConcreteComplex .
    var stP stPr : KeyStoragePath .

    *** Right Part in Storage
    ceq < sLs <- stC ; prog > = < Ks <- stC ; sLs <- Ks ; prog >
        if not (sLs :: KeyStoragePath) .
    eq < stP <- sRR,a ; prog > = { stP := sRR : a }< prog > .

    *** Right Part Concrete in Storage
    eq < sLs <- stC,aC ; prog > = < Ks <- stC ; sLs <- Ks,aC ; prog > .
    eq < cL <- kcC ; prog > = < K <- kcC ; cL <- K ; prog > .
    eq < sL <- ksStorB,aC ; prog > = { sL := select(Storage, ksStorB : aC) } < prog > .


    *** Left Part Complex in Storage
    eq < stC,a <- kval ; prog > = < Ks <- stC ; Ks,a <- kval ; prog > .

    *** Left Part Simple in Storage
    eq < ksStorB,aC <- val ; prog > = { Storage := store(Storage, ksStorB : aC, val) }< prog > .
    eq < ksStorB,aS <- ksStorR ; prog > = { K := select(Storage, ksStorR) } { Storage := store(Storage, ksStorB, K) } < prog > .
    eq < stP <- cR,a ; prog > = < Ks <- cR ; stP <- Ks,a ; prog > .
    eq < ksStorL <- ksStorB ; prog > = { K := select(Storage, ksStorB) } { Storage := store(Storage, ksStorL, K) } < prog > .

    *** Storage paths
    eq < stP <- stPr ; prog > = { stP := stPr } < prog > .
endfm

fmod DYNAMIC-TEST is
    pr DYNAMIC .
    ops subAccount account : -> FieldStorage .
    ops balance : -> FieldConcrete .
    ops accS alice carol : -> KeySimpleStorage .
    ops v : -> KeySimpleConcrete .
    op x0 : -> Value .
    ops alicePath bobPath accPath : -> KeyStoragePath .
endfm

red < accS,balance <- x0 ; carol,account <- accS ; v <- carol,account,balance > .
red < carol,account,balance <- x0 ; alice,account <- carol,account ; v <- alice,account,balance > .
red < alice,account,subAccount <- carol,account,subAccount > .
red < alice,account,subAccount <- carol,account > .

red < accS <- carol,account > .
red < carol,account <- accS > .
red < carol,account,balance <- x0 > .
red < accS,balance <- x0 > .
red < alice,account,balance <- accS,balance > .
red < v <- carol,account,balance > .

red < alicePath <- bobPath > .
red < alicePath <- bobPath,account > .
red < alicePath,account <- accPath > .
